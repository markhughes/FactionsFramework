import org.apache.tools.ant.filters.*

plugins {
    id "com.jfrog.bintray" version "1.5"
}
apply plugin: "maven"

repositories {
    mavenLocal()
    jcenter()
}

allprojects {
    apply plugin: "application"
    apply plugin: "java"

    repositories {
        mavenLocal()
        jcenter()

        maven {
            url "https://hub.spigotmc.org/nexus/content/groups/public/"
        }

        maven {
            name "BungeeCord"
            url "https://oss.sonatype.org/content/repositories/snapshots"
        }
    }

    dependencies {
        compile group: "org.spigotmc", name: "spigot-api", version: project.property("apiversion"), { ext { fatJarExclude = true } }
    }

    mainClassName = "me.markeh.factionsframework.FactionsFramework"

    processResources {
        filter ReplaceTokens, tokens: [
            "version": project.property("version")
        ]
    }

}

task copyResources {
    outputs.file new File("$buildDir/plugin.yml")
    doLast {
        copy {
            from "plugin.yml"
            into "$buildDir"
        }
    }
}

version = project.property("version");

subprojects.each { subproject -> evaluationDependsOn( subproject.path ) }
jar.dependsOn subprojects.tasks["classes"]
jar {
    baseName = "FactionsFramework"
    applicationDistribution.from(copyResources) {
        into ""
    }

    manifest {
        attributes "Main-Class": "me.markeh.factionsframework.FactionsFramework"
    }

    subprojects.each { subproject ->
        from subproject.sourceSets.main.output.classesDir
        from subproject.sourceSets.main.output.resourcesDir
    }
}
// ----------------------------------------
// BINTRAY
// ----------------------------------------

// jCenter requires a pom.xml file
task createPom << {
    pom {
        project {
            groupId "me.markeh.factionsframework"
            artifactId "factionsframework"
            version project.property("version")
            inceptionYear "2016"

            licenses {
                license {
                    name "GNU General Public License, version 3 (GPL-3.0)"
                    url "https://opensource.org/licenses/GPL-3.0"
                    distribution "repo"
                }
            }
        }
    }.writeTo("build/libs/pom.xml")
}

// Create sources jar
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Create javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Deploy to bintray
bintray {
	user = project.hasProperty("bintrayUser") ? project.property("bintrayUser") : System.getenv("BINTRAY_USER")
	key = project.hasProperty("bintrayApiKey") ? project.property("bintrayApiKey") : System.getenv("BINTRAY_API_KEY")
    filesSpec {
		from "build/libs"
		into "."
	}
	pkg {
		repo = "maven"
		name = "FactionsFramework"
		desc = ""
        userOrg = "redstoneore"
		licenses = ["GPL-3.0"]
		vcsUrl = "https://github.com/MarkehMe/FactionsFramework.git"
		publicDownloadNumbers = false
		version {
			name = "FactionsFramework-" + project.property("version");
			desc = "Build a Factions plugin for every version of Factions and not have to worry about version upgrades and changes."
			vcsTag = project.property("version")
			attributes = ["gradle-plugin": "com.use.less:com.use.less.gradle:gradle-useless-plugin"]
		}
	}
}
